const std = @import("std");
const builtin = @import("builtin");

pub fn build(b: *std.Build) !void {
    const target = b.standardTargetOptions(.{});
    const optimize = b.standardOptimizeOption(.{});

    const options = .{
        // TODO: statically linked libGL
        .backend_opengl = b.option(bool, "opengl", "Enable support for the OpenGL graphics API") orelse false,
        // TODO: statically linked vulkan-loader
        .backend_vulkan = b.option(bool, "vulkan", "Enable support for the Vulkan graphics API") orelse false,
        .backend_cairo = b.option(bool, "cairo", "Enable support for the Cairo graphics API") orelse false,
        .backend_stub = b.option(bool, "stub", "Build stub backend") orelse false,

        // TODO: statically linked X11 libs
        .use_xcursor = b.option(bool, "xcursor", "Support changing the cursor on X11") orelse true,
        .use_xrandr = b.option(bool, "xrandr", "Support accessing the refresh rate on X11") orelse true,
        .use_xsync = b.option(bool, "xsync", "Support timers on X11") orelse true,
        .win_wchar = b.option(bool, "win_wchar", "Use UTF-16 wchar_t and UNICODE with Windows API") orelse true,
    };

    // Cross-compile system paths, passed explicitly rather than via --sysroot or
    // --search-prefix: both of those are graph-wide, so they also hit native host-tool
    // steps in the same build graph (e.g. this package's own opengl-generator, which
    // then fails with "unable to find libSystem system library"), and --search-prefix
    // never reaches translate-c.
    const cross_paths = .{
        .include = b.option(std.Build.LazyPath, "include_path", "Target system include path (for cross-compiling)"),
        .framework = b.option(std.Build.LazyPath, "framework_path", "Target system framework path (for cross-compiling to macOS)"),
        .library = b.option(std.Build.LazyPath, "library_path", "Target system library path (for cross-compiling)"),
    };

    const options_step = b.addOptions();
    inline for (std.meta.fields(@TypeOf(options))) |option| {
        options_step.addOption(option.type, option.name, @field(options, option.name));
    }

    const platform: enum { x11, mac, win } = switch (target.result.os.tag) {
        .linux, .freebsd, .openbsd, .netbsd, .dragonfly => .x11,
        .macos => .mac,
        .windows => .win,
        else => |p| std.debug.panic("unsupported platform: {}", .{p}),
    };
    options_step.addOption(@TypeOf(platform), "platform", platform);
    const c_src_ext = if (platform == .mac) "m" else "c";

    var c_flags = std.ArrayList([]const u8).empty;
    try c_flags.appendSlice(b.allocator, &.{ "-DPUGL_INTERNAL", "-DPUGL_STATIC" });

    var tests = std.ArrayList([]const u8).empty;

    const pugl_dep = b.dependency("pugl", .{});

    const pugl_c = b.addTranslateC(.{
        .target = target,
        .optimize = optimize,
        .root_source_file = pugl_dep.path("include/pugl/pugl.h"),
    });
    pugl_c.addIncludePath(pugl_dep.path("include"));
    const pugl_c_module = pugl_c.addModule("c");

    const pugl = b.addModule("pugl", .{
        .target = target,
        .optimize = optimize,
        .root_source_file = b.path("src/pugl/pugl.zig"),
        .link_libc = true,
        .imports = &.{
            .{ .name = "pugl_options", .module = options_step.createModule() },
            .{ .name = "c", .module = pugl_c_module },
        },
    });

    const lib = b.addLibrary(.{
        .name = "pugl",
        .root_module = pugl,
    });
    b.installArtifact(lib);

    pugl.linkSystemLibrary("m", .{});

    pugl.addIncludePath(pugl_dep.path("include"));
    pugl.addIncludePath(pugl_dep.path("subprojects/puglutil/include"));
    lib.installHeadersDirectory(pugl_dep.path("include"), "", .{});
    lib.installHeadersDirectory(pugl_dep.path("subprojects/puglutil/include"), "", .{});

    switch (platform) {
        .x11 => {
            // Cross-compiling to Linux from a non-Linux host: the host's pkg-config
            // (e.g. Homebrew's on macOS) resolves X11/GL to host-arch libs. These come
            // from plain absolute -Dinclude_path/-Dlibrary_path options (NOT
            // --sysroot): a global --sysroot also applies to native host-tool compiles
            // elsewhere in the build graph (e.g. this package's own opengl-generator)
            // and breaks those ("unable to find libSystem system library"), so
            // headers/libs are supplied directly instead.
            const cross_linux = builtin.os.tag != .linux;
            const use_pkg_config: std.Build.Module.SystemLib.UsePkgConfig = if (cross_linux) .no else .yes;
            if (cross_linux) {
                if (cross_paths.include) |p| pugl.addSystemIncludePath(p);
                if (cross_paths.library) |p| pugl.addLibraryPath(p);
                if (cross_paths.include == null or cross_paths.library == null) {
                    std.debug.print("error: cross-compiling to Linux requires -Dinclude_path and -Dlibrary_path pointing at a Linux sysroot's usr/include and usr/lib (X11/GL headers+libs)\n", .{});
                    std.process.exit(1);
                }
            }

            pugl.linkSystemLibrary("X11", .{ .use_pkg_config = use_pkg_config });
            pugl.linkSystemLibrary("Xrender", .{ .use_pkg_config = use_pkg_config });

            try c_flags.append(b.allocator, "-D_POSIX_C_SOURCE=200809L");

            if (options.use_xcursor) {
                pugl.linkSystemLibrary("Xcursor", .{ .use_pkg_config = use_pkg_config });
                try c_flags.append(b.allocator, "-DUSE_XCURSOR=1");
            }

            if (options.use_xrandr) {
                pugl.linkSystemLibrary("Xrandr", .{ .use_pkg_config = use_pkg_config });
                try c_flags.append(b.allocator, "-DUSE_XRANDR=1");
            }

            if (options.use_xsync) {
                pugl.linkSystemLibrary("Xext", .{ .use_pkg_config = use_pkg_config });
                try c_flags.append(b.allocator, "-DUSE_XSYNC=1");
            }
        },
        .mac => {
            // Native macOS builds need nothing here: clang locates the system SDK itself.
            if (cross_paths.include) |p| pugl.addSystemIncludePath(p);
            if (cross_paths.framework) |p| pugl.addSystemFrameworkPath(p);
            if (cross_paths.library) |p| pugl.addLibraryPath(p);
            if (builtin.os.tag != .macos and (cross_paths.include == null or cross_paths.framework == null or cross_paths.library == null)) {
                std.debug.print("error: cross-compiling to macOS requires -Dinclude_path, -Dframework_path and -Dlibrary_path pointing at a macOS SDK's usr/include, System/Library/Frameworks and usr/lib\n", .{});
                std.process.exit(1);
            }

            pugl.linkFramework("Cocoa", .{});
            pugl.linkFramework("CoreVideo", .{});
        },
        .win => {
            try c_flags.appendSlice(b.allocator, &.{
                "-DWINVER=0x0500", // Windows 2000
                "-D_WIN32_WINNT=0x0500", // Windows 2000
                // Disable as many things from windows.h as possible
                "-DWIN32_LEAN_AND_MEAN",
                "-DNOGDICAPMASKS", // CC_*, LC_*, PC_*, CP_*, TC_*, RC_
                "-DNOSYSMETRICS", // SM_*
                "-DNOKEYSTATES", // MK_*
                "-DOEMRESOURCE", // OEM Resource values
                "-DNOATOM", // Atom Manager routines
                "-DNOCOLOR", // Screen colors
                "-DNODRAWTEXT", // DrawText() and DT_*
                "-DNOKERNEL", // All KERNEL defines and routines
                "-DNOMB", // MB_* and MessageBox()
                "-DNOMEMMGR", // GMEM_*, LMEM_*, GHND, LHND, associated routines
                "-DNOMETAFILE", // typedef METAFILEPICT
                "-DNOMINMAX", // Macros min(a,b) and max(a,b)
                "-DNOOPENFILE", // OpenFile(), OemToAnsi, AnsiToOem, and OF_*
                "-DNOSCROLL", // SB_* and scrolling routines
                "-DNOSERVICE", // All Service Controller routines, SERVICE_ equates, etc.
                "-DNOSOUND", // Sound driver routines
                "-DNOWH", // SetWindowsHook and WH_*
                "-DNOCOMM", // COMM driver routines
                "-DNOKANJI", // Kanji support stuff
                "-DNOHELP", // Help engine interface
                "-DNOPROFILER", // Profiler interface
                "-DNODEFERWINDOWPOS", // DeferWindowPos routines
                "-DNOMCX", // Modem Configuration Extensions
            });
            if (options.win_wchar)
                try c_flags.appendSlice(b.allocator, &.{ "-DUNICODE", "-D_UNICODE" });
            pugl.linkSystemLibrary("user32", .{});
            pugl.linkSystemLibrary("shlwapi", .{});
            pugl.linkSystemLibrary("dwmapi", .{});
            pugl.linkSystemLibrary("gdi32", .{});
        },
    }

    const backend_imports: []const std.Build.Module.Import = &.{
        .{ .name = "pugl", .module = pugl },
        .{ .name = "c", .module = pugl_c_module },
    };

    if (options.backend_opengl) {
        switch (platform) {
            // "GL" (not lowercase "gl") to match the actual libGL.so/.a name on Linux.
            .x11 => pugl.linkSystemLibrary("GL", .{
                .use_pkg_config = if (builtin.os.tag != .linux) .no else .yes,
            }),
            .win => pugl.linkSystemLibrary("opengl32", .{}),
            else => {},
        }

        pugl.addCSourceFile(.{ .file = pugl_dep.path(b.fmt("src/{s}_gl.{s}", .{ @tagName(platform), c_src_ext })) });

        const opengl_c = b.addTranslateC(.{
            .target = target,
            .optimize = optimize,
            .root_source_file = pugl_dep.path("include/pugl/gl.h"),
        });
        opengl_c.addIncludePath(pugl_dep.path("include"));
        opengl_c.defineCMacro("PUGL_NO_INCLUDE_GL_H", "1");

        const opengl_module = b.addModule("backend_opengl", .{
            .root_source_file = b.path("src/backend/opengl.zig"),
            .imports = backend_imports,
        });
        opengl_module.addImport("opengl_c", opengl_c.createModule());

        try tests.appendSlice(b.allocator, &.{
            "gl",
            "gl_free_unrealized",
            "gl_hints",
        });
    }

    if (options.backend_vulkan) {
        pugl.linkSystemLibrary("vulkan", .{});

        pugl.addCSourceFile(.{
            .file = pugl_dep.path(b.fmt("src/{s}_vulkan.{s}", .{ @tagName(platform), c_src_ext })),
            .flags = c_flags.items,
        });

        if (platform == .mac) {
            pugl.linkFramework("Metal", .{});
            pugl.linkFramework("QuartzCore", .{});
        }

        const vulkan_c = b.addTranslateC(.{
            .target = target,
            .optimize = optimize,
            .root_source_file = pugl_dep.path("include/pugl/vulkan.h"),
        });
        vulkan_c.addIncludePath(pugl_dep.path("include"));

        const vulkan_module = b.addModule("backend_vulkan", .{
            .root_source_file = b.path("src/backend/vulkan.zig"),
            .imports = backend_imports,
        });
        vulkan_module.addImport("vulkan_c", vulkan_c.createModule());

        try tests.append(b.allocator, "vulkan");
    }

    if (options.backend_cairo) {
        if (b.lazyDependency("cairo", .{
            .target = target,
            .optimize = optimize,
            .use_zlib = false,
            .use_xcb = false,
            .symbol_lookup = false,
            .use_glib = false,
        })) |cairo| {
            if (b.systemIntegrationOption("cairo", .{}))
                pugl.linkSystemLibrary("cairo", .{})
            else
                pugl.linkLibrary(cairo.artifact("cairo"));

            b.addNamedLazyPath("cairo_headers", cairo.namedWriteFiles("headers").getDirectory());
        }

        pugl.addCSourceFile(.{
            .file = pugl_dep.path(b.fmt("src/{s}_cairo.{s}", .{ @tagName(platform), c_src_ext })),
            .flags = c_flags.items,
        });

        const cairo_c = b.addTranslateC(.{
            .target = target,
            .optimize = optimize,
            .root_source_file = pugl_dep.path("include/pugl/cairo.h"),
        });
        cairo_c.addIncludePath(pugl_dep.path("include"));

        const cairo_module = b.addModule("backend_cairo", .{
            .root_source_file = b.path("src/backend/cairo.zig"),
            .imports = backend_imports,
        });
        cairo_module.addImport("cairo_c", cairo_c.createModule());
        cairo_module.linkLibrary(lib);

        try tests.append(b.allocator, "cairo");
    }

    if (options.backend_stub) {
        pugl.addCSourceFile(.{
            .file = pugl_dep.path(b.fmt("src/{s}_stub.{s}", .{ @tagName(platform), c_src_ext })),
            .flags = c_flags.items,
        });

        const stub_c = b.addTranslateC(.{
            .target = target,
            .optimize = optimize,
            .root_source_file = pugl_dep.path("include/pugl/stub.h"),
        });
        stub_c.addIncludePath(pugl_dep.path("include"));

        const stub_module = b.addModule("backend_stub", .{
            .root_source_file = b.path("src/backend/stub.zig"),
            .imports = backend_imports,
        });
        stub_module.addImport("stub_c", stub_c.createModule());

        try tests.appendSlice(b.allocator, &.{
            "cursor",
            "realize",
            "redisplay",
            "show_hide",
            "size",
            "strerror",
            "stub",
            "stub_hints",
            "update",
            "view",
            "world",
            "local_copy_paste",
            "remote_copy_paste",
            "timer",
        });
    }

    pugl.addCSourceFiles(.{
        .root = pugl_dep.path("src"),
        .files = &.{
            b.fmt("{s}.{s}", .{ @tagName(platform), c_src_ext }),
            "common.c",
            "internal.c",
        },
        .flags = c_flags.items,
    });

    const run_tests_step = b.step("test", "Run tests");

    const unit_tests = b.addTest(.{
        .root_module = pugl,
    });

    const run_unit_tests = b.addRunArtifact(unit_tests);
    run_tests_step.dependOn(&run_unit_tests.step);

    for (tests.items) |test_name| {
        const test_exe = b.addExecutable(.{
            .name = b.fmt("test_{s}", .{test_name}),
            .root_module = b.createModule(.{
                .target = target,
                .optimize = optimize,
            }),
        });
        test_exe.root_module.addCSourceFile(.{ .file = pugl_dep.path(b.fmt("test/test_{s}.c", .{test_name})) });
        test_exe.root_module.linkLibrary(lib);

        const run_test = b.addRunArtifact(test_exe);
        run_tests_step.dependOn(&run_test.step);
    }

    const docs_step = b.step("docs", "Build API docs");

    const install_docs = b.addInstallDirectory(.{
        .source_dir = lib.getEmittedDocs(),
        .install_dir = .prefix,
        .install_subdir = "docs",
    });
    docs_step.dependOn(&install_docs.step);
}
